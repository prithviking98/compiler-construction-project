1. <program> ===> <otherFunctions> <mainFunction>
2. <mainFunction>===> TK_MAIN <stmts> TK_END
3. <otherFunctions>===> <function><otherFunctions> | eps
4. <function>===>TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
5. <input_par>===>TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
6. <output_par>===>TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR |eps
7. <parameter_list>===><dataType> TK_ID <remaining_list>
8. <dataType>===> <primitiveDatatype> |<constructedDatatype>
9. <primitiveDatatype>===> TK_INT | TK_REAL
10. <constructedDatatype>===>TK_RECORD TK_RECORDID
11. <remaining_list>===>TK_COMMA <parameter_list> | eps
12. <stmts>===><typeDefinitions> <declarations> <otherStmts><returnStmt>
13. <typeDefinitions>===><typeDefinition><typeDefinitions> |eps
14. <typeDefinition>===>TK_RECORD TK_RECORDID <fieldDefinitions> TK_ENDRECORD TK_SEM
15. <fieldDefinitions>===> <fieldDefinition><fieldDefinition><moreFields>
16. <fieldDefinition>===> TK_TYPE <primitiveDatatype> TK_COLON TK_FIELDID TK_SEM
17. <moreFields>===><fieldDefinition><moreFields> | eps
18. <declarations> ===> <declaration><declarations>|eps
19. <declaration>===> TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM
20. <global_or_not>===>TK_COLON TK_GLOBAL| eps
21. <otherStmts>===> <stmt><otherStmts> | eps
22. <stmt>===> <assignmentStmt> | <iterativeStmt>|<conditionalStmt>|<ioStmt>| <funCallStmt>
23. <assignmentStmt>===><singleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
24. <singleOrRecId>===>TK_ID <new_24>
25. <new_24> ===> eps | TK_DOT TK_FIELDID
26. <funCallStmt>===><outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
27. <outputParameters> ==> TK_SQL <idList> TK_SQR TK_ASSIGNOP | eps
28. <inputParameters>===> TK_SQL <idList> TK_SQR
29. <iterativeStmt>===> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt><otherStmts> TK_ENDWHILE
30. <conditionalStmt>===> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt><otherStmts> <elsePart>
31. <elsePart>===>TK_ELSE <stmt><otherStmts> TK_ENDIF | TK_ENDIF
32. <ioStmt>===> TK_READ TK_OP <singleOrRecId> TK_CL TK_SEM| TK_WRITE TK_OP <allVar> TK_CL TK_SEM
33. <allVar>===>TK_NUM|TK_RNUM|TK_ID<allVarTemp>
34. <allVarTemp>===>TK_DOT TK_FIELDID|eps
35. <arithmeticExpression> ===> <term> <expPrime>
36. <expPrime> ===> <lowPrecedenceOperators> <term> <expPrime> | eps
37. <term>===> <factor> <termPrime>
38. <termPrime> ===> <highPrecedenceOperators><factor> <termPrime> | eps
39. <factor> ===> TK_OP <arithmeticExpression> TK_CL | <all>
40. <highPrecedenceOperators>===> TK_MUL | TK_DIV
41. <lowPrecedenceOperators> ===> TK_PLUS | TK_MINUS
42. <all>===> TK_ID<temp>|TK_NUM|TK_RNUM
43. <temp>===> eps | TK_DOT TK_FIELDID
44. <booleanExpression>===>TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL| <var> <relationalOp> <var>|TK_NOT TK_OP <booleanExpression>TK_CL
45. <var>===> TK_ID|TK_NUM|TK_RNUM
46. <logicalOp>===>TK_AND | TK_OR
47. <relationalOp>===> TK_LT | TK_LE | TK_EQ |TK_GT | TK_GE | TK_NE
48. <returnStmt>===>TK_RETURN <optionalReturn> TK_SEM
49. <optionalReturn>===>TK_SQL <idList> TK_SQR | eps
50. <idList>===> TK_ID <more_ids>
51. <more_ids>===> TK_COMMA <idList> | eps
